Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLANIM_Internal_RegisterMod();
KBSECTION
//REGION DEBUG
IF
GameModeStarted(_GameMode, 1)
AND
NOT DB_Origins(_)
THEN
DB_Origins((CHARACTERGUID)S_GLO_CharacterCreationDummy_001_da072fe7-fdd5-42ae-9139-8bd4b9fca406);
DB_LLANIM_PartyTemplates("Dwarves_Hero_Female_c1c58707-b06e-499e-9c43-91a90be602b0", "Class_Knight_Dwarves");
DB_LLANIM_PartyTemplates("Dwarves_Hero_Male_024d1763-b2aa-46ec-b705-6338059838be", "Class_Knight_Dwarves");
DB_LLANIM_PartyTemplates("Elves_Hero_Female_7ef846f5-34dc-450c-815e-a58dfc190a7b", "Class_Knight_Elves");
DB_LLANIM_PartyTemplates("Elves_Hero_Male_19913083-924e-45ec-8b5b-119d5913722f", "Class_Knight_Elves");
DB_LLANIM_PartyTemplates("Humans_Hero_Female_de8ea39b-6989-4b93-b34a-81e549c540f2", "Class_Knight_Humans");
DB_LLANIM_PartyTemplates("Lizards_Hero_Female_e4a6bcfa-ecd6-4e56-8592-cd16b85a1c50", "Class_Knight_Lizards");
DB_LLANIM_PartyTemplates("Lizards_Hero_Male_fa12e21f-0a10-47dd-af46-ab2c9a53cf6d", "Class_Knight_Lizards");

IF
TextEventSet("llanim_bonetest")
AND
GetTextEventParamString(1, _Bone)
AND
NOT GetTextEventParamString(2, _)
AND
CharacterGetHostCharacter(_Host)
THEN
PlayEffect(_Host, "RS3_FX_GP_ScriptedEvent_ChanelledSourceOrb_01", _Bone);

IF
TextEventSet("llanim_bonetest")
AND
GetTextEventParamString(1, _Bone)
AND
GetTextEventParamString(2, _Effect)
AND
CharacterGetHostCharacter(_Host)
THEN
PlayEffect(_Host, _Effect, _Bone);

IF
TextEventSet("llanim_party")
AND
SysCount("DB_IsPlayer", 1, _Count)
AND
_Count > 1
AND
DB_IsPlayer(_Char)
THEN
CharacterAddSkill(_Char, "Rush_LLANIM_Debug_Whirlwind", 0);
CharacterAddSkill(_Char, "Rush_LLANIM_Debug_RushSmash", 0);

IF
TextEventSet("llanim_party")
AND
SysCount("DB_IsPlayer", 1, 1)
AND
CharacterGetHostCharacter(_Host)
AND
GetPosition(_Host, _x, _y, _z)
AND
DB_LLANIM_PartyTemplates(_Template, _Equipment)
AND
CharacterCreateAtPosition(_x, _y, _z, _Template, 0, _Char)
THEN
DB_IsPlayer(_Char);
CharacterMakePlayer(_Char, _Host);
CharacterAddSkill(_Char, "Rush_LLANIM_Debug_Whirlwind", 0);
CharacterAddSkill(_Char, "Rush_LLANIM_Debug_RushSmash", 0);
CharacterTransformAppearanceToWithEquipmentSet(_Char, _Char, _Equipment, 0);
//END_REGION

//REGION VERSION_UPDATE
IF
GameStarted(_Level, _)
AND
IsGameLevel(_Level, 1)
AND
NOT DB_LLANIM_Version(268959744)
THEN
LLANIM_Internal_RegisterMod();

PROC
LLANIM_Internal_RegisterMod()
THEN
LLANIM_Updater_RemoveOldVersions(268959744);
LLANIM_Updater_SetVersion(268959744, "1.0.8.0");
//END_REGION

//REGION LEADERLIB_VERSIONING
PROC
LLANIM_Updater_RemoveOldVersions((INTEGER)_NextVersion)
AND
DB_Mods_Registered("AnimationsPlus", "LaughingLeader", _Version)
THEN
NOT DB_Mods_Registered("AnimationsPlus", "LaughingLeader", _Version);

PROC
LLANIM_Updater_RemoveOldVersions((INTEGER)_NextVersion)
AND
NOT DB_LLANIM_Version(_)
THEN
LLANIM_Updater_VersionUpdated(-1, _NextVersion);

PROC
LLANIM_Updater_RemoveOldVersions((INTEGER)_NextVersion)
AND
DB_LLANIM_Version(_LastVersion)
THEN
NOT DB_LLANIM_Version(_LastVersion);
LLANIM_Updater_VersionUpdated(_LastVersion, _NextVersion);

PROC
LLANIM_Updater_SetVersion((INTEGER)_NextVersion, (STRING)_VersionString)
AND
GlobalGetFlag("LeaderLib_Initialized", 1)
THEN
//Fire LeaderLib ModUpdated procs.
DB_LeaderLib_ModApi_RegisterMod("AnimationsPlus", "LaughingLeader", _VersionString);

PROC
LLANIM_Updater_SetVersion((INTEGER)_NextVersion, (STRING)_VersionString)
AND
GlobalGetFlag("LeaderLib_Initialized", 0)
THEN
DB_Mods_Registered("AnimationsPlus", "LaughingLeader", _VersionString);

PROC
LLANIM_Updater_SetVersion((INTEGER)_NextVersion, (STRING)_VersionString)
THEN
DB_LLANIM_Version(_NextVersion);
//END_REGION

//REGION UPDATES
PROC
LLANIM_Updater_VersionUpdated((INTEGER)_LastVersion, (INTEGER)_NextVersion)
AND
_LastVersion < 268959744
THEN
LLANIM_RegisterLeaderLibSettings();
//END_REGION

//REGION LEADERLIB_REGISTER_SETTINGS
IF
StoryEvent(_, "LeaderLib_Initialized")
AND
NOT DB_LLANIM_RegisteredLeaderLibSettings(_)
THEN
DB_LLANIM_RegisteredLeaderLibSettings(1);
LLANIM_RegisterLeaderLibSettings();

PROC
LLANIM_RegisterLeaderLibSettings()
THEN
DB_LeaderUpdater_RegisteredUUID("AnimationsPlus", "LaughingLeader", 326b8784-edd7-4950-86d8-fcae9f5c457c);
DB_LeaderLib_ModApi_RegisterActiveGoal("AnimationsPlus", "LaughingLeader", "LaughingLeader_AnimationsPlus");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__Start"
